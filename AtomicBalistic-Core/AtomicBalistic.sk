#===================================================================================================================================#
#        _      _                         _          ____            _   _         _     _        
#       / \    | |_    ___    _ __ ___   (_)   ___  | __ )    __ _  | | (_)  ___  | |_  (_)   ___ 
#      / _ \   | __|  / _ \  | '_ ` _ \  | |  / __| |  _ \   / _` | | | | | / __| | __| | |  / __|
#     / ___ \  | |_  | (_) | | | | | | | | | | (__  | |_) | | (_| | | | | | \__ \ | |_  | | | (__ 
#    /_/   \_\  \__|  \___/  |_| |_| |_| |_|  \___| |____/   \__,_| |_| |_| |___/  \__| |_|  \___|
#
#                               By Vengelis_
#
#===================================================================================================================================#                                                                                              

on load:
    log "&9[%now% INFO] &7Chargement du fichier de configuration 'AtomicBalistic-Config.yml'..."
    if file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" doesn't exist:
        log "&6[%now% WARN] &7Création du fichier de configuration"
        load yaml "plugins/AtomicBalistic/AtomicBalistic-Config.yml"
        
        #   set yml value "" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to ""
        
        #   Core
        
        set yml value "AtomicBalistic.core.general.prefix" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "&7&l[&c&lAtomic&6Balistic&7&l] &8>"
        set yml value "AtomicBalistic.core.general.name" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "AtomicBalistic"
        
        #   Permissions
        
        set yml value "AtomicBalistic.core.permissions.admin" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "AtomicBalistic.admin"
        set yml value "AtomicBalistic.core.permissions.mod" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "AtomicBalistic.mod"
        set yml value "AtomicBalistic.core.permissions.player" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "AtomicBalistic.player"
        set yml value "AtomicBalistic.core.permissions.deny" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "&4Erreur ! &cVous n'avez pas la permission d'executer cette commande !"
        
        #   AutoUpdater
        
        set yml value "AtomicBalistic.core.autoUpdater.enable" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "true"
        set yml value "AtomicBalistic.core.autoUpdater.forcoming" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "true"
        
        #   Modules
        
        set yml value "AtomicBalistic.Modules.ICBM.enable" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "true"
        set yml value "AtomicBalistic.Modules.ParticulesAccelerator.enable" from file "plugins/AtomicBalistic/AtomicBalistic-Config.yml" to "false"
        
    load yaml "plugins/AtomicBalistic/AtomicBalistic-Config.yml"
    log "&f[%now% &9INFO&f] Configuration chargée"
    
on unload:
    log "&9[%now% INFO] &7AtomicBalistic Déchargé !"

function ICBMreset(parameter: text):
    if {_parameter} is "all":
        delete {ICBM::*}
        set {ICBM::Counter} to 0
        loop all players:
            delete {icbmBaseClicked.%loop-player%}
    if {_parameter} is "silo":
        delete {ICBM::ListOfSilo::*}
    if {_parameter} is "freqency":
        delete {ICBM::ListOfSilo::Frequency::*} 
        delete {ICBM::ListOfSilo::FrequencyPassword::*}
    
command /AtomicBalisticAdmin [<text>] [<text>] [<text>] [<text>]:
    aliases: aba
    trigger:
        set {_AS::CoreLoad::Logo} to (yaml value "AtomicBalistic.core.general.prefix" of "AtomicBalistic-Config")
        set {_AS::CoreLoad::EnabledIcbm} to (yaml value "AtomicBalistic.Modules.ICBM.enable" of "AtomicBalistic-Config")
        set {_AS::CoreLoad::EnabledPA} to (yaml value "AtomicBalistic.Modules.ParticulesAccelerator.enable" of "AtomicBalistic-Config")
        set {_AS::CoreLoad::AdminPerm::permission} to (yaml value "AtomicBalistic.core.permissions.admin" of "AtomicBalistic-Config")
        set {_AS::CoreLoad::AdminPerm::denyMessage} to (yaml value "AtomicBalistic.core.permissions.deny" of "AtomicBalistic-Config")
        if player has permission "%{_AS::CoreLoad::AdminPerm::permission}%":
            if arg-1 is not set:
                message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cVous devez spécifier un/des argument(s) &7-> /AtomicBalisticAdmin help"
            if arg-1 is "help":
                message "&7---------- &8[ &6AtomicBalistic &8] &7----------"
                message "   &9Page d'aide aux commandes admins"
                message ""
                message "&a/AtomicBalisticAdmin modules add <nom du module> &7Ajouter un module"
                message "&a/AtomicBalisticAdmin modules remove <nom du module> &7Retirer un module"
                message "&a/AtomicBalisticAdmin modules reload <nom du module> &7Recharger un module"
                message "&a/AtomicBalisticAdmin modules list &7liste des modules"
                message "&a/AtomicBalisticAdmin ICBM reset <all/silo/frequences> &7Reset le module ICBM suivant les paramètres"
                message "&a/AtomicBalisticAdmin ICBM giveMissile <list/nom du missile> &7Give un missile"
            if arg-1 is "modules":
                if arg-2 is "add":
                    if {AS::Core::Modules::%arg-3%} is set:
                        message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cModule déjà intégré !"
                    else:
                        if file "plugins/Skript/scripts/AtomicBalistic/-%arg-3%.sk" doesn't exist:
                            message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cModule non trouvé !"
                            wait 1 ticks
                            if arg-3 is "ICBM":
                                message "%{_AS::CoreLoad::Logo}% &7Téléchragement du module '&eICBM&7' via internet..."
                                wait 1 ticks
                                delete file "plugins/Skript/scripts/AtomicBalistic/-ICBM.sk"
                                create file "plugins/Skript/scripts/AtomicBalistic/-ICBM.sk"
                                download from "https://pastebin.com/raw/df170hWY" to "plugins/Skript/scripts/AtomicBalistic/-ICBM.sk"
                                message "%{_AS::CoreLoad::Logo}% &7Module téléchargé !"
                                delete file "plugins/AtomicBalistic/ICBM.yml"
                                create file "plugins/AtomicBalistic/ICBM.yml"
                                download from "https://pastebin.com/raw/3wqfzx2M" to "plugins/AtomicBalistic/ICBM.yml"
                                message "%{_AS::CoreLoad::Logo}% &7Fichier de configuration d'&eICBM &7téléchargé ! &8(Configuration de: Vengelis_)"
                                make console execute command "sk enable AtomicBalistic/ICBM"
                                set {AS::Core::Modules::%arg-3%} to "&aICBM"
                            stop
                        else:
                            set {AS::Core::Modules::%arg-3%} to "&aICBM"
                            message "%{_AS::CoreLoad::Logo}% &7Module &e%arg-3% &7en cours d'ajout..."
                            make console execute command "sk enable AtomicBalistic/%arg-3%"
                            message "%{_AS::CoreLoad::Logo}% &7Module &e%arg-3% &7ajouté !"
                            stop
                else if arg-2 is "remove":
                    if {AS::Core::Modules::%arg-3%} is set:
                        delete {AS::Core::Modules::%arg-3%}
                        delete {AB::VarList::%arg-3%::*}
                        message "%{_AS::CoreLoad::Logo}% &7Supression des variables..."
                        ICBMreset("all")
                        message "%{_AS::CoreLoad::Logo}% &7Désactivation du module..."
                        make console execute command "sk disable AtomicBalistic/%arg-3%"
                        message "%{_AS::CoreLoad::Logo}% &7Module &e%arg-3% &7retiré !"
                        stop
                    else:
                        message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cModule non trouvé !"
                        stop
                else if arg-2 is "reload":
                    if {AS::Core::Modules::%arg-3%} is set:
                        message "%{_AS::CoreLoad::Logo}% &7Rechargement du module..."
                        wait 1 ticks
                        make console execute command "sk reload AtomicBalistic/%arg-3%"
                        message "%{_AS::CoreLoad::Logo}% &7Module &e%arg-3% &7rechargé !"
                        stop
                    else:
                        message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cModule non trouvé !"
                        stop
                else if arg-2 is "list":
                    message "%{_AS::CoreLoad::Logo}% &7Liste des modules ajoutés:"
                    message "&a%{AS::Core::Modules::*}%"
                    stop
                else if arg-2 is "statsVar":
                    if arg-3 is set:
                        if {AS::Core::Modules::%arg-3%} is set:
                            message "%{_AS::CoreLoad::Logo}% &7Liste des variables fixes:"
                            message "&a%{AB::VarList::%arg-3%::*}%"
                            stop
                else:
                    message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cAction impossible ! &7-> /AtomicBalisticAdmin help"
                    stop
            if arg-1 is "ICBM":
                if {_AS::CoreLoad::EnabledIcbm} is "true":
                    if {AS::Core::Modules::%arg-1%} is set:
                        if arg-2 is "reset":
                            if arg-3 is "all":
                                message "%{_AS::CoreLoad::Logo}% &7Reset total d'ICBM en cours..."
                                ICBMreset("all")
                                message "%{_AS::CoreLoad::Logo}% &7Reset terminé !"
                                stop
                            else if arg-3 is "silo":
                                message "%{_AS::CoreLoad::Logo}% &7Reset des silos en cours..."
                                ICBMreset("silo")
                                message "%{_AS::CoreLoad::Logo}% &7Reset terminé !"
                                stop
                            else if arg-3 is "frequences":
                                message "%{_AS::CoreLoad::Logo}% &7Reset des fréquences en cours..."
                                ICBMreset("frequency")
                                message "%{_AS::CoreLoad::Logo}% &7Reset terminé !"
                                stop
                            else:
                                message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cAction impossible ! &7-> /AtomicBalisticAdmin help"
                                stop
                        else if arg-2 is "giveMissile":
                            if arg-3 is "list":
                                load yaml "plugins/AtomicBalistic/ICBM.yml"
                                if arg-4 is not set:
                                    set {_p} to 1
                                else:
                                    set {_p} to arg-4 parsed as integer
                                    if {_p} is not an integer:
                                        set {_p} to 1
                                loop yaml nodes "ICBM.Missile" of "ICBM":
                                    add loop-value to {_missiles::*}
                                set {_mp} to ceil((size of {_missiles::*} / 36))
                                if {_p} > {_mp}:
                                    make player execute command "/aba icbm givemissile list %{_mp}%"
                                    stop
                                else if {_p} < 1:
                                    make player execute command "/aba icbm givemissile list 1"
                                    stop
                                open virtual chest inventory with size 6 named "Missiles" to player
                                set {_i} to 0
                                loop {_missiles::*}:
                                    if {_i} >= {_p} * 36 - 36:
                                        if {_i} < {_p} * 36:
                                            set {_mis} to loop-value
                                            format gui slot {_i} - 36 * ({_p} - 1) of player with lever named "&7Missile &e%loop-value%" to run:
                                                add a arrow named "&6%{_mis}%" to the player's inventory
                                                message "%{_AS::CoreLoad::Logo}% &7Don du missile &e%{_mis}%"
                                    add 1 to {_i}
                                if {_p} != {_mp}:
                                    format gui slot 50 of player with an arrow named "&ePage suivante" to run player command "/aba icbm givemissile list %({_p} + 1)%"
                                if {_p} != 1:
                                    format gui slot 48 of player with an arrow named "&ePage précédente" to run player command "/aba icbm givemissile list %({_p} - 1)%"
                                format gui slot 49 of player with sign item named "&ePage Actuelle: &f%{_p}%" with lore "&eNombre de pages: &f%{_mp}%" to do nothing
                                format gui slot 36, 37, 38, 39, 40, 41, 42, 43 and 44 of player with gray glass pane named "&r" to do nothing
                                format gui slot 53 of player with a barrier named "&cFermer" to close
                                stop
                            else if arg-3 is set:
                                add a arrow named "&6%arg-3%" to the player's inventory
                                message "%{_AS::CoreLoad::Logo}% &7Don du missile &e%arg-3%"
                                stop
                            else:
                                message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cAction impossible ! &7-> /AtomicBalisticAdmin help"
                                stop
                        else if arg-2 is "Config":
                            if arg-3 is "Download":
                                if arg-4 is "Vengelis":
                                    delete file "plugins/AtomicBalistic/ICBM.yml"
                                    create file "plugins/AtomicBalistic/ICBM.yml"
                                    download from "https://pastebin.com/raw/3wqfzx2M" to "plugins/AtomicBalistic/ICBM.yml"
                                if arg-4 is "Exemple":
                                    delete file "plugins/AtomicBalistic/ICBM.yml"
                                    create file "plugins/AtomicBalistic/ICBM.yml"
                                    download from "https://pastebin.com/raw/1h3Ufdwc" to "plugins/AtomicBalistic/ICBM.yml"
                                message "%{_AS::CoreLoad::Logo}% &7Configuration ICBM de &e%arg-4% &7téléchargée !"
                                stop
                    else:
                        message "%{_AS::CoreLoad::Logo}% &4Erreur ! Module pas intégré !"
                        stop
                else:
                    message "%{_AS::CoreLoad::Logo}% &4Erreur ! Module désactivé !"
                    stop
            if arg-1 is "reload" or "relaod":
                if arg-2 is "viewLog":
                    message "%{_AS::CoreLoad::Logo}% &7Rechargement d'AtomicBalistic en cours..."
                    make player execute command "sk reload AtomicBalistic"
                    message "%{_AS::CoreLoad::Logo}% &7Rechargement terminé !"
                else:
                    message "%{_AS::CoreLoad::Logo}% &7Rechargement d'AtomicBalistic en cours..."
                    make console execute command "sk reload AtomicBalistic"
                    message "%{_AS::CoreLoad::Logo}% &7Rechargement terminé !"
            else:
                message "%{_AS::CoreLoad::Logo}% &4Erreur ! &cAction impossible ! &7-> /AtomicBalisticAdmin help"
                stop
on load:
    log "&a[%now% SUCESS] &7AtomicBalistic full chargé"
